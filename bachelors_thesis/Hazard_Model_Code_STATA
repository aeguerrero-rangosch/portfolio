/*===============================================================================

Date Created: December 29th, 2019
Last Revision: April 15th, 2020
Author: Alessandra Guerrero

Project: Hyperinflation Honors Thesis
Purpose: Hazard Model Analysis + Robustness Checks

==============================================================================*/

clear all
set more off

use "D:\Hyperinflations\Data & Code\Clean Data\hazard_ready_data.dta"

*===============================================================================
* 							PRELIMINARY ANALYSIS 
*===============================================================================

// Listing STSET Data and Basic Graphing----------------------------------------
sts list			// listing the cumulative hazard estimates by spell length


// Getting a Estimated Function-------------------------------------------------
sts graph, cumhaz		// graphing the cumulative hazard function estimates
sts graph, survival 	// graphing the survival function estimates
sts graph, hazard width(1)	// graphing a smooth hazard function (bins = 1)
sts graph, risktable 		// graphing hazard function with numbers at risk

sts gen s = s			// generate KM survival function
	label variable s "K-M Survivor Function"
sts gen cumh = na		// generate cumulative hazard function
	label variable cumh "N-A Cumulative Hazard Function"
sts gen deltach = h		// generate the hazard contribution 
	label variable deltach "Hazard Contribution"

// Plotting Estimates Against Survival Time-------------------------------------
twoway line s _t, sort connect(J) 		// plot KM survival function by time
twoway line cumh _t, sort connect(J)	// plot NA cumulative hazard by time
twoway line deltach _t, sort connect(J) // plot hazard contribution by time

// Verifying Hazard Function----------------------------------------------------
egen tagt = tag(_t)						 // generate time tags
	sort _t								 // sort by time
	list _t deltach cumh s if tagt == 1  // list hazard contribution by time
	
// Checking If Data Continuous or Discrete (just for posterity)-----------------
ltable length hyper, graph				 // graphing outcome
	ltable length hyper, graph noadjust	 // graphing outcome without adjusting

ltable length hyper, hazard				 // tabling outcome by hazard function
	ltable length hyper, hazard noadjust // tabling outcome without adjusting
	
// Functional Form of Hazard Function-------------------------------------------

// Specifications:
gen logseqvar = ln(seqvar)	// log of time interval variable
gen seqvarsq = seqvar^2		// time interval variable squared
gen seqvarcb = seqvar^3		// time interval variable cubed

// For Non-Parametric Baseline:
tabulate seqvar, gen(d)		// generate dummies for each time interval
tabulate seqvar hyper		// tabulate time intervals against outcomes

*===============================================================================
* 							PARING DOWN MODEL
*===============================================================================

// Testing of Proportional Hazard-----------------------------------------------
quietly: stcox hyper wb_* icrg* kaopen	// cox regression for preliminary model

stcox icrg*, mgale(mgale) // regression only with ICRG variables
stcoxgof 				  // goodness of fit of cox with only ICRG variables
	drop mgale			  // drop martingale residuals
		
stcox wb_* icrg*,  mgale(mgale) // regression with WB and ICRG variables
stcoxgof						// goodness of fit with WB and ICRG variables
	drop mgale					// drop martingale residuals
				
stcox wb_* icrg* kaopen			// regression with WB, ICRG, and KAOPEN vars.
	test wb_1					// testing each individual WB variable
	test wb_2
	test wb_9
	test wb_10
	//... test all wb_* variables
	// drop wb_1 and wb_16, not significant for regression
	test icrg1 					// testing each individual IRCG variable
	test icrg2
	//... test all icrg* variable
	// drop icrg6, not significant for regression
	test kaopen 				// testing KAOPEN variable
		
stcox wb_* icrg* kaopen, mgale(mgale) // testing goodness of fit of final model
stcoxgof 			// high p-value indicates we have a good model fit
					// this is our final model
			
// Cheking Cox Regressions------------------------------------------------------
stcox wb_* icrg* kaopen			// cox regression with all variables
eststo reg6

stcox wb_* kaopen if e(sample)	// cox regression with WB and KAOPEN variables
eststo reg5						// paring down sample to match
		
stcox wb_* icrg* if e(sample)	// cox regression with WB and ICRG variables
eststo reg4						// paring down sample to match

stcox wb_* if e(sample)			// cox regression only with WB variables
eststo reg3						// paring down sample to match

lrtest reg3 reg4	// test of additional var. significance (significant)
lrtest reg3 reg5	// test of additional var. significance (significant)
lrtest reg3 reg6	// test of additional var. significance (significant)

esttab reg3 reg4 reg5 reg6 using log2.tex, replace ///
											eform ///
											compress ///
											booktabs ///
											se ///
											bic ///
											scalars(chi2) ///
											coeflabels(Hazard Ratios) ///
											longtable ///
											title(Hazard Ratios)


estat phtest, detail					// proportional-hazard test
	mat R = r(phtest)					// exporting to latex code
	mat g = .a, r(chi2), r(df), r(p)
	mat rowname g = "Global Test"
	mat R = R\g
	mat l R 
	esttab mat(R), tex 					
		
*===============================================================================
* 							ESTIMATION ANALYSIS 
*===============================================================================
	
// Static Logistic--------------------------------------------------------------
logit hyper wb_* icrg* kaopen, or	// static logit 
eststo reg1
esttab reg1 using log1.tex,	replace ///
							compress ///
							booktabs ///
							se ///
							pr2 ///
							coeflabels(Odds Ratios) ///
							eform ///
							longtable ///
							title(Static Logistic Regression (All Covariates))					

// Continuous-Time--------------------------------------------------------------
streg wb_* icrg* kaopen, distribution(exponential) // survival time regression
eststo reg2										   // exponential distribution
			
// Discrete-Time----------------------------------------------------------------
logit hyper wb_* icrg* kaopen, or // dynamic time-varying logit 
	
glm hyper wb_* icrg* kaopen, family(binomial) link(cloglog) eform 
eststo reg8					 // logit (GLM) with cloglog link
	
cloglog hyper wb_* icrg* kaopen, eform	// cloglog regression 
eststo reg9
	
esttab reg1 reg2 reg6 reg8 reg9 using log2.tex, replace ///
												label ///
												eform ///
												compress ///
												booktabs ///
												se ///
												bic ///
												scalars(chi2) ///
												coeflabels(Hazard Ratios) ///
												longtable ///
												title(Hazard Ratios)

			
// Unobserved Heterogeneity-----------------------------------------------------
cloglog hyper wb_* icrg* kaopen logseqvar, nolog	// cloglog regression
xtcloglog hyper wb_* icrg* kaopen logseqvar, nolog i(countryid)
	// time-dependent cloglog with frailty analysis 
xtlogit hyper wb_* icrg* kaopen logseqvar, i(countryid) nolog
	// time-dependent logit with frailty analysis
	predict p, pu0			// predicting probabilities of failure
	bysort countryid (seqvar): gen ss = exp(sum(ln(1-p)))
							// generating out of sample probabilities

	
